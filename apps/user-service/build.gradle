plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '7.2.1'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.epages.restdocs-api-spec' version '0.18.2'
}

group = 'site.icebang'
version = '0.1.0-SNAPSHOT'
description = 'Ice bang - fast campus team4'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	// AsciiDoctor Extension for REST Docs
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// MyBatis
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'

	// Scheduler
	implementation 'org.springframework.boot:spring-boot-starter-quartz'

	// Retry
	implementation 'org.springframework.retry:spring-retry'

	// logging
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
	implementation 'org.apache.logging.log4j:log4j-layout-template-json'
	implementation 'pl.tkowalcz.tjahzi:log4j2-appender-nodep:0.9.17'
	implementation 'org.apache.httpcomponents:httpclient:4.5.14'
	implementation 'org.apache.httpcomponents:httpcore:4.4.16'


	// micrometer & actuator
	implementation "io.micrometer:micrometer-tracing-bridge-brave"
	implementation "io.micrometer:micrometer-tracing"
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "io.micrometer:context-propagation"

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	// Database
	runtimeOnly 'com.h2database:h2'
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.3'

	// Development dependencies
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

	// Test Dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mariadb'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Spring REST Docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'
}

// REST Docs 스니펫 디렉토리 설정
ext {
	snippetsDir = file('build/generated-snippets')
}

tasks.register('unitTest', Test) {
	outputs.dir snippetsDir
	useJUnitPlatform {
		includeTags 'unit'
	}

	systemProperty 'spring.profiles.active', 'test-unit'
}

tasks.register('integrationTest', Test) {
	outputs.dir snippetsDir
	useJUnitPlatform {
		includeTags 'integration'
	}

	systemProperty 'spring.profiles.active', 'test-integration'

	timeout = Duration.ofMinutes(10)
}

// E2E 테스트 전용 task 추가
tasks.register('e2eTest', Test) {
	outputs.dir snippetsDir
	useJUnitPlatform {
		includeTags 'e2e'
	}

	systemProperty 'spring.profiles.active', 'test-e2e'

	// E2E 테스트는 더 긴 시간 허용
	timeout = Duration.ofMinutes(10)
}

// AsciiDoctor 설정 (REST Docs 문서 생성)
asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test

	baseDirFollowsSourceDir()

	attributes(
			'snippets': snippetsDir,
			'source-highlighter': 'coderay',
			'toc': 'left',
			'toclevels': '3',
			'sectlinks': 'true',
			'operation-curl-request-title': 'Example request',
			'operation-http-response-title': 'Example response'
	)
}

asciidoctor.doFirst {
	delete file('src/docs/asciidoc')
}

// JAR에 생성된 문서 포함
bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}

	// 프로덕션 JAR에서 불필요한 파일들 제외
	exclude 'application-test-*.yml'
	exclude 'log4j2-test-*.yml'
	exclude 'application-develop.yml'
	exclude 'log4j2-*.yml'
	exclude 'sql/**'
}

spotless {
	java {
		googleJavaFormat('1.17.0')
		importOrder('java', 'javax', 'org', 'com', '', 'site.icebang')
		endWithNewline()
		removeUnusedImports()
		encoding('UTF-8')

		targetExclude("**/generated/**", "**/Q*.java")
	}
	format 'misc', {
		target '**/*.gradle', '**/*.md', '**/.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
}

openapi3 {
	server = 'http://localhost:8080'
	title = 'IceBang API'
	description = 'IceBang API Documentation'
	version = '0.0.1-alpha-snapshot'
	format = 'yaml'
}
