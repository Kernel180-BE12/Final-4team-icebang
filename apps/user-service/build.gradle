plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '7.2.1'
}

group = 'com.gltkorea'
version = '0.0.1-alpha-SNAPSHOT'
description = 'GLT korea - fast campus team4 ice bang'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	// Spring Boot의 기본 로깅(Logback) 제외
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// MyBatis
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'

	// Log4j2 - 모든 모듈을 2.22.1로 통일
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
	implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
	implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.22.1'
	implementation 'org.apache.logging.log4j:log4j-jul:2.22.1'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

	// 비동기 로깅
	implementation 'com.lmax:disruptor:3.4.4'

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	// Database
	runtimeOnly 'com.h2database:h2'
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.3'

	// Test Dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mariadb'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform {
		// 기본적으로는 e2e 태그 제외하고 실행
		excludeTags 'e2e'
	}
	systemProperty 'spring.profiles.active', 'test-unit'
}

// E2E 테스트 전용 task 추가
tasks.register('e2eTest', Test) {
	useJUnitPlatform {
		includeTags 'e2e'
	}

	systemProperty 'spring.profiles.active', 'test-e2e'

	// E2E 테스트는 더 긴 시간 허용
	timeout = Duration.ofMinutes(10)
}

// 모든 테스트 실행 task
tasks.register('allTests', Test) {
	useJUnitPlatform()
}

spotless {
	java {
		googleJavaFormat('1.17.0')
		importOrder('java', 'javax', 'org', 'com', '', 'com.movement')
		endWithNewline()
		removeUnusedImports()
		encoding('UTF-8')

		targetExclude("**/generated/**", "**/Q*.java")
	}
	format 'misc', {
		target '**/*.gradle', '**/*.md', '**/.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
}
